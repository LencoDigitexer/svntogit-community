# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=bitwarden_rs-web
pkgver=2.18.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with bitwarden_rs'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('bitwarden_rs')
makedepends=('git' 'npm' 'python2')
provides=('bitwarden_rs-vault')
replaces=('bitwarden_rs-vault')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$pkgver"
        'git+https://github.com/bitwarden/jslib.git'
        "$pkgname-v$pkgver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$pkgver/patches/v$pkgver.patch")
b2sums=('SKIP'
        'SKIP'
        '3d90e14ec7e6fbb2db482b6bbbfb5dd95f4f9fbde25276b36334dbc937d0c8486442a6b9d37123dacfd24cdf48f5aec42c5526f0b2f7cc2ca8bbe3465dad69cb')

prepare() {
  cd bitwarden-web
  git submodule init
  git config submodule.jslib.url ../jslib
  git submodule update
  patch --forward --strip=1 --input=../$pkgname-v$pkgver.patch
}

build() {
  cd bitwarden-web
  npm install --frozen-lockfile
  npm audit fix
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/$pkgname
  cp -R build/* "$pkgdir"/usr/share/$pkgname
}

# vim:set ts=2 sw=2 et:
