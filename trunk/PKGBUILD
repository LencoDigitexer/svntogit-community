# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=badtouch
pkgver=0.7.2
pkgrel=1
pkgdesc='Scriptable network authentication cracker'
url='https://github.com/kpcyrd/badtouch'
depends=('openssl' 'lua52' 'libcap' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('GPL3')
install=badtouch.install
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha256sums=('f3e384ccd9ff90c19f081c8cb240aff28bac42e7b318b5a04f50a70fc75537a4'
            'SKIP')
sha512sums=('1d20db2f45e52018ece0c34d8eb8ee32c89ef14aa73632d88ce48bb0a5645b9e66fec3645b68da12a980cbc4f31f7200601612f598835c7b8345edc50979a317'
            'SKIP')
validpgpkeys=('64B13F7117D6E07D661BBCE0FE763A64F5E54FD6')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/badtouch" completions bash > "${pkgdir}/usr/share/bash-completion/completions/badtouch"
  "${pkgdir}/usr/bin/badtouch" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_badtouch"
  "${pkgdir}/usr/bin/badtouch" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/badtouch.fish"

  install -Dm 644 docs/badtouch.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 scripts/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
